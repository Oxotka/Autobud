#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУзелОбмена();
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗагрузка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ВыполнитьЗагрузку", 0.2, Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ВыполнитьЗагрузкуИзУправлениеТорговлей();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредупрежденияНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.РезультатыОбменаДанными.Форма.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизацияДанныхНажатие(Элемент)
	
	ОткрытьФорму("ОбщаяФорма.СинхронизацияДанных");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗагрузку()
	
	ВыполнитьЗагрузкуИзУправлениеТорговлей();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗагрузкуИзУправлениеТорговлей()
	
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан узел обмена", ,"УзелОбмена");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = НачатьЗагрузкуИзУправлениеТорговлей(УзелОбмена);
	Если ДлительнаяОперация.Статус = "Ошибка" Тогда
		
		ПоказатьОшибкуЗагрузки(ДлительнаяОперация);
		
	Иначе
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
		НастройкиОжидания.ПолучатьРезультат = Истина;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("Подключаемый_ПослеЗагрузки", ЭтотОбъект),
			НастройкиОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НачатьЗагрузкуИзУправлениеТорговлей(УзелОбмена)
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка из Управление торговлей'");
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ЗагрузкаИзУправлениеТорговли.ВыполнитьЗагрузкуВФоне",
		УзелОбмена,
		НастройкиЗапуска);
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеЗагрузки(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		Отказ = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультат;
		Если Отказ Тогда
			РезультатЗагрузки = "Загрузка не выполнена.
			|Обратитесь к администратору!";
		Иначе
			РезультатЗагрузки = "Загрузка выполнена успешно!";
		КонецЕсли;
	
		Элементы.СинхронизацияДанных.Видимость = Отказ;
		Элементы.Предупреждения.Видимость = НЕ Отказ;
		
	Иначе
		ПоказатьОшибкуЗагрузки(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуЗагрузки(ДлительнаяОперация)
	
	Текст = НСтр("ru = 'Ошибка при загрузке данных из Управление торговлей:'");
	Предупреждение = Новый Структура;
	Предупреждение.Вставить("Текст", Текст + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки);
	Предупреждение.Вставить("Подробно", ДлительнаяОперация.ПодробноеПредставлениеОшибки);
	СтандартныеПодсистемыКлиент.ВывестиПредупреждение(ЭтотОбъект, Предупреждение);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУзелОбмена()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел = ЛОЖЬ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		УзелОбмена = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти