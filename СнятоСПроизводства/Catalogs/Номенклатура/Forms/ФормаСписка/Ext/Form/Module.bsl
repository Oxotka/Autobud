#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Снято_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// 1. Снятые с производства детали выделяем зачеркнутым шрифтом.
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СписокСтандартныйПоискНоменклатура");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СписокРасширенныйПоискНоменклатура");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатура.АБ_СнятоСПроизводства");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(255,0,0));
	
	//
	
	УстановитьОтборПоСнятоСПроизводства(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура Снято_ПриЗагрузкеДанныхИзНастроекНаСервереПеред(Настройки)
	
	УстановитьОтборПоСнятоСПроизводства(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Снято_ПоказыватьСнятыеСПроизводстваПриИзмененииПеред(Элемент)
	
	УстановитьОтборПоСнятоСПроизводства(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСнятоСПроизводства(Форма)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Форма.СписокНоменклатура,
		"АБ_СнятоСПроизводства",
		Ложь,
		НЕ Форма.ПоказыватьСнятыеСПроизводства);
	
КонецПроцедуры

#КонецОбласти