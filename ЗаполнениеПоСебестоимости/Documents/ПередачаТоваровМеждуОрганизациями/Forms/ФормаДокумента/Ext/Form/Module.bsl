
&НаКлиенте
Процедура Себестоимость_ЗаполнитьЦеныПоСебестоимостиПеред(Команда)
	Себестоимость_ЗаполнитьЦеныПоСебестоимостиПередНаСервере();
КонецПроцедуры

&НаСервере
Процедура Себестоимость_ЗаполнитьЦеныПоСебестоимостиПередНаСервере()
	
	ТаблицаТовары = РеквизитФормыВЗначение("Объект.Товары");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВТ_Товары",     ТаблицаТовары.Выгрузить());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("КонецПериода",  Объект.Дата);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Серия
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ВТ_Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НомерСтроки,
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Серия,
		|	АналитикаУчетаНоменклатуры.КлючАналитики
		|ПОМЕСТИТЬ АналитикаУчета
		|ИЗ
		|	ТаблицаНоменклатуры КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|		ПО Товары.Номенклатура = АналитикаУчетаНоменклатуры.Номенклатура
		|			И Товары.Характеристика = АналитикаУчетаНоменклатуры.Характеристика
		|			И Товары.Серия = АналитикаУчетаНоменклатуры.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АналитикаУчета.НомерСтроки,
		|	АналитикаУчета.Номенклатура,
		|	АналитикаУчета.Характеристика,
		|	АналитикаУчета.Серия,
		|	СУММА(СебестоимостьТоваровОстаткиИОбороты.КоличествоКонечныйОстаток) КАК КоличествоОстаток,
		|	СУММА(СебестоимостьТоваровОстаткиИОбороты.СтоимостьКонечныйОстаток) КАК СтоимостьОстаток,
		|	СУММА(СебестоимостьТоваровОстаткиИОбороты.КоличествоПриход) КАК КоличествоПриход,
		|	СУММА(СебестоимостьТоваровОстаткиИОбороты.СтоимостьПриход) КАК СтоимостьПриход
		|ПОМЕСТИТЬ ВТ_Себестоимость
		|ИЗ
		|	АналитикаУчета КАК АналитикаУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Месяц, , ) КАК СебестоимостьТоваровОстаткиИОбороты
		|		ПО АналитикаУчета.КлючАналитики = СебестоимостьТоваровОстаткиИОбороты.АналитикаУчетаНоменклатуры
		|
		|СГРУППИРОВАТЬ ПО
		|	АналитикаУчета.НомерСтроки,
		|	АналитикаУчета.Номенклатура,
		|	АналитикаУчета.Характеристика,
		|	АналитикаУчета.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Себестоимость.НомерСтроки,
		|	ВТ_Себестоимость.Номенклатура,
		|	ВТ_Себестоимость.Характеристика,
		|	ВТ_Себестоимость.Серия,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВТ_Себестоимость.КоличествоОстаток, 0) <> 0
		|			ТОГДА ЕСТЬNULL(ВТ_Себестоимость.СтоимостьОстаток, 0) / ВТ_Себестоимость.КоличествоОстаток
		|		КОГДА ЕСТЬNULL(ВТ_Себестоимость.КоличествоПриход, 0) <> 0
		|			ТОГДА ЕСТЬNULL(ВТ_Себестоимость.СтоимостьПриход, 0) / ВТ_Себестоимость.КоличествоПриход
		|		ИНАЧЕ ЕСТЬNULL(ВТ_Себестоимость.СтоимостьОстаток, 0)
		|	КОНЕЦ КАК Себестоимость
		|ИЗ
		|	ВТ_Себестоимость КАК ВТ_Себестоимость";
	
	ТаблицаССебестоимостью = Запрос.Выполнить().Выгрузить();
	ТаблицаССебестоимостью.Индексы.Добавить("НомерСтроки");
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВСтрокеТЧ(Объект);
	СтруктураДействий = Новый Структура( // Структура действий с измененными строками
		"ПересчитатьСумму, ПересчитатьСуммуСНДС, ПересчитатьСуммуНДС",
		"КоличествоУпаковок", СтруктураПересчетаСуммы, СтруктураПересчетаСуммы);
	
	КэшированныеЗначения = Неопределено;
	
	Для Каждого СтрокаТаблицыТовары Из ТаблицаТовары Цикл
		СтруктураОтбора = Новый Структура("НомерСтроки", СтрокаТаблицыТовары.НомерСтроки);
		СтрокаСебестоимости = ТаблицаССебестоимостью.НайтиСтроки(СтруктураОтбора)[0];
		Множитель = ?(СтрокаСебестоимости.Себестоимость < 0, -1, 1);
		СтрокаТаблицыТовары.Цена = СтрокаСебестоимости.Себестоимость * Множитель;
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицыТовары, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
КонецПроцедуры
