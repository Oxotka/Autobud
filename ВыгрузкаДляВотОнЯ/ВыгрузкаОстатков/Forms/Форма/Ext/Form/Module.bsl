#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыЦен.Ссылка
		|ИЗ
		|	Справочник.ВидыЦен КАК ВидыЦен";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ВидЦены = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
	BUYER     = "0000000000000";
	SUPPLIER  = "0000000000000";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КаталогВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ПустаяСтрока(Элемент.ТекстРедактирования) Тогда
		Файл = Новый Файл(Элемент.ТекстРедактирования);
		Если Не Файл.Существует() Тогда
			ТекстПредупреждения = НСтр("ru = 'Невозможно открыть папку, так как она не существует.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		ЗапуститьПриложение(Элемент.ТекстРедактирования);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Открытие окна выбора папки сохранения.
	СтандартнаяОбработка = Ложь;
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ВыборФайла.МножественныйВыбор = Ложь;
	ВыборФайла.Каталог = Элемент.ТекстРедактирования;
	Если ВыборФайла.Выбрать() Тогда
		КаталогВыгрузки = ВыборФайла.Каталог + ПолучитьРазделительПути();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьОстатки(Команда)
	
	КаталогВыгрузкиДляПроверки = Новый Файл(КаталогВыгрузки);
	
	Если Не КаталогВыгрузкиДляПроверки.Существует() Тогда
		ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Папка ""%1"" не найдена.
			           |Выберите другую папку.'"),
			КаталогВыгрузки));
		Возврат;
	КонецЕсли;
	
	// 1. Остатки выгружаются в XML файл в кодировке UTF-8.
	// 2. Имя файла должно быть вида STOCAT_YYYYMMDD_HHMMSS.XML
	// 3. Расширение файла XML указывать обязательно.
	ДатаВыгрузки = ТекущаяДата();
	ПутьКФайлу = КаталогВыгрузки + "STOCAT_" + Формат(ДатаВыгрузки,"ДФ=yyyyMMdd") + "_" + Формат(ДатаВыгрузки,"ДФ=hhmmss") + ".xml";
	Запись = Новый ЗаписьXML;
	Запись.ОткрытьФайл(ПутьКФайлу, "UTF-8"); 
	Запись.ЗаписатьОбъявлениеXML(); //<?xml version="1.0" encoding="UTF-8"?>
	Запись.ЗаписатьНачалоЭлемента("STOCAT");// Stocat начало.
	
	ЗаписатьЭлементXML(Запись, "DATE", Формат(ДатаВыгрузки,"ДФ=yyyy-MM-dd"));
	ЗаписатьЭлементXML(Запись, "TIME", Формат(ДатаВыгрузки,"ДФ=HH:mm"));
	ЗаписатьЭлементXML(Запись, "STOCKDATE", Формат(ДатаВыгрузки,"ДФ=yyyy-MM-dd"));
	
	ЗаписатьЭлементXML(Запись, "INFO", Комментарий);

	Запись.ЗаписатьНачалоЭлемента("HEAD");// HEAD начало.
	
	ЗаписатьЭлементXML(Запись, "SUPPLIER",     SUPPLIER);
	ЗаписатьЭлементXML(Запись, "BUYER",        BUYER);
	ЗаписатьЭлементXML(Запись, "SENDER",       SUPPLIER);
	ЗаписатьЭлементXML(Запись, "RECIPIENT",    BUYER);
#Область Position
	РассчитатьОстаткиТоваров(ДатаВыгрузки);
	ПорядковыйНомер = 1;
	Для Каждого Стр Из ОстаткиНоменклатуры Цикл
		Запись.ЗаписатьНачалоЭлемента("POSITION");// POSITION начало.
		ЗаписатьЭлементXML(Запись, "POSITIONNUMBER",    Формат(ПорядковыйНомер,"ЧГ="));
		ЗаписатьЭлементXML(Запись, "PRODUCT",           Штрихкод(Стр.ШтрихКод));
		ЗаписатьЭлементXML(Запись, "PRODUCTIDBUYER",    Стр.Артикул);
		ЗаписатьЭлементXML(Запись, "PRODUCTIDSUPPLIER", Стр.Артикул);
		ЗаписатьЭлементXML(Запись, "DESCRIPTION",       Стр.НаименованиеПолное);
		ЗаписатьЭлементXML(Запись, "UNIT",              Строка(Стр.ЕдиницаИзмерения));
		ЗаписатьЭлементXML(Запись, "STOCKQUANTITY",     Формат(Стр.Остаток, "ЧДЦ=2; ЧС=; ЧРД=.; ЧН=0.00; ЧГ="));
		ЗаписатьЭлементXML(Запись, "PRICE",             Формат(Стр.Цена, "ЧДЦ=2; ЧС=; ЧРД=.; ЧН=0.00; ЧГ="));
		ЗаписатьЭлементXML(Запись, "NETPRICE",          Формат(Стр.ЦенаБезНДС, "ЧДЦ=2; ЧС=; ЧРД=.; ЧН=0.00; ЧГ="));
		ЗаписатьЭлементXML(Запись, "VAT",               Формат(Стр.СтавкаНДСЧислом, "ЧДЦ=2; ЧС=; ЧРД=.; ЧН=0.00; ЧГ="));
		Запись.ЗаписатьКонецЭлемента();// POSITION конец.
		ПорядковыйНомер = ПорядковыйНомер + 1;
	КонецЦикла;
#КонецОбласти
	
	Запись.ЗаписатьКонецЭлемента();// HEAD конец.
	
	Запись.ЗаписатьКонецЭлемента();// Stocat конец.
	
	Запись.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПутьКФайлуНастроек()
	
	Возврат "C:\Настройки.xml";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаписатьЭлементXML(ЗаписьXML, ИмяЭлемента, ЗначениеЭлемента)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	ЗаписьXML.ЗаписатьТекст(СокрЛП(ЗначениеЭлемента));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьОстаткиТоваров(ДатаВыгрузки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	СпрНоменклатура.Артикул,
		|	СпрНоменклатура.НаименованиеПолное,
		|	СпрНоменклатура.ЕдиницаИзмерения,
		|	СпрНоменклатура.СтавкаНДС,
		|	ШтрихкодыНоменклатуры.Штрихкод
		|ПОМЕСТИТЬ ВТ_Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО (ШтрихкодыНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
		|ГДЕ
		|	&ОтборПоНоменклатуре
		|	И СпрНоменклатура.ЭтоГруппа = ЛОЖЬ
		|	И СпрНоменклатура.ПометкаУдаления = ЛОЖЬ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Номенклатура.Номенклатура,
		|	ВТ_Номенклатура.Артикул,
		|	ВТ_Номенклатура.НаименованиеПолное,
		|	ВТ_Номенклатура.ЕдиницаИзмерения,
		|	ВТ_Номенклатура.СтавкаНДС,
		|	ВТ_Номенклатура.Штрихкод,
		|	СвободныеОстаткиОстатки.ВНаличииОстаток КАК Остаток,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ВидЦены, НЕОПРЕДЕЛЕНО) = НЕОПРЕДЕЛЕНО
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.ВидЦены.ЦенаВключаетНДС
		|	КОНЕЦ КАК ЦенаВключаетНДС,
		|	0 КАК НДС,
		|	0 КАК ЦенаБезНДС,
		|	0 КАК СтавкаНДСЧислом
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(
		|				&Период,
		|				&Склад = НЕОПРЕДЕЛЕНО
		|					ИЛИ Склад В ИЕРАРХИИ (&Склад)) КАК СвободныеОстаткиОстатки
		|		ПО ВТ_Номенклатура.Номенклатура = СвободныеОстаткиОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ВТ_Номенклатура.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура";
		
	Запрос.УстановитьПараметр("Период",  ДатаВыгрузки);
	Запрос.УстановитьПараметр("Склад",   ?(ЗначениеЗаполнено(Склад), Склад, Неопределено));
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Если ОтборНоменклатуры.Количество() = 0 Тогда
		Запрос.УстановитьПараметр("ОтборПоНоменклатуре", Истина);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоНоменклатуре", "СпрНоменклатура.Родитель В ИЕРАРХИИ(&ОтборНоменклатуры)");
		Запрос.УстановитьПараметр("ОтборНоменклатуры", ОтборНоменклатуры);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОстатков = РезультатЗапроса.Выгрузить();
	Для Каждого Стр Из ТаблицаОстатков Цикл
		Стр.СтавкаНДСЧислом = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(Стр.СтавкаНДС);
		Стр.НДС = Ценообразование.РассчитатьСуммуНДС(Стр.Цена, Стр.СтавкаНДС, Стр.ЦенаВключаетНДС);
		Стр.ЦенаБезНДС = Стр.Цена - Стр.НДС;
	КонецЦикла;
	ОстаткиНоменклатуры.Загрузить(ТаблицаОстатков);
	
КонецПроцедуры

&НаКлиенте
Функция Штрихкод(Знач Штрихкод)
	
	Штрихкод = СокрЛП(?(ШтрихКод = Null, "", ШтрихКод));
	Возврат ?(СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Штрихкод), Штрихкод, "");
	
КонецФункции

&НаКлиенте
Процедура СохранитьНастройкиВФайл(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.ПолноеИмяФайла = "Настройки.xml";
	Диалог.Фильтр = НСтр("ru = 'Все файлы (*.xml)|*.xml'");
	Диалог.Заголовок = "Укажите файл сохранения настроек";
	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьНастройкиВФайлЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайлЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = Результат[0];
	СохранитьНастройкиВФайлНаСервере(ИмяФайла);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВФайлНаСервере(ИмяФайла)
	
	Запись = Новый ЗаписьXML;
	Запись.ОткрытьФайл(ИмяФайла, "UTF-8");
	Запись.ЗаписатьОбъявлениеXML();
	Запись.ЗаписатьНачалоЭлемента("Настройки");
	ЗаписатьЭлементXML(Запись, "BUYER",             ОбщегоНазначения.ЗначениеВСтрокуXML(BUYER));
	ЗаписатьЭлементXML(Запись, "SUPPLIER",          ОбщегоНазначения.ЗначениеВСтрокуXML(SUPPLIER));
	ЗаписатьЭлементXML(Запись, "ВидЦены",           ОбщегоНазначения.ЗначениеВСтрокуXML(ВидЦены));
	ЗаписатьЭлементXML(Запись, "Склад",             ОбщегоНазначения.ЗначениеВСтрокуXML(Склад));
	ЗаписатьЭлементXML(Запись, "ОтборНоменклатуры", ОбщегоНазначения.ЗначениеВСтрокуXML(ОтборНоменклатуры));
	Запись.ЗаписатьКонецЭлемента();
	Запись.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьНастройкиИзФайла(Команда)
	
	ПрочитатьНастройкиИзФайлаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиИзФайлаНаСервере()
	
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьФайл(ПутьКФайлуНастроек());
	СтруктураНастроек = Новый Структура;
	ИмяКлюча = "";
	Пока Чтение.Прочитать() Цикл
		Если Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если Чтение.Имя = "Настройки" Тогда
				Продолжить;
			КонецЕсли;
			ИмяКлюча = Чтение.Имя;
		ИначеЕсли Чтение.ТипУзла=ТипУзлаXML.Текст Тогда
			СтруктураНастроек.Вставить(ИмяКлюча, ОбщегоНазначения.ЗначениеИзСтрокиXML(Чтение.Значение));
		ИначеЕсли Чтение.ТипУзла=ТипУзлаXML.КонецЭлемента Тогда
			ИмяКлюча = "";
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураНастроек);
	
КонецПроцедуры

#КонецОбласти
