#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУзелОбмена();
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжидание;
		ВыполнитьЗагрузку();
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗагрузка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ВыполнитьЗагрузку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредупрежденияНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.РезультатыОбменаДанными.Форма.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизацияДанныхНажатие(Элемент)
	
	ОткрытьФорму("ОбщаяФорма.СинхронизацияДанных");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьЗагрузку()
	
	Если НЕ ЗначениеЗаполнено(УзелОбмена) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан узел обмена", ,"УзелОбмена");
		Возврат;
	КонецЕсли;
	
	// Обмен
	ПараметрыОбмена = ОбменДаннымиСервер.ПараметрыОбмена();
	ПараметрыОбмена.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.FILE;
	ПараметрыОбмена.ВыполнятьЗагрузку = Истина;
	ПараметрыОбмена.ВыполнятьВыгрузку = Истина;
	
	ПараметрыОбмена.ДлительнаяОперацияРазрешена = Ложь;
	ПараметрыОбмена.ДлительнаяОперация          = Ложь;
	
	Отказ = Ложь;
	ОбменДаннымиСервер.ВыполнитьОбменДаннымиДляУзлаИнформационнойБазы(УзелОбмена, ПараметрыОбмена, Отказ);
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультат;
	Если Отказ Тогда
		РезультатЗагрузки = "Загрузка не выполнена.
		|Обратитесь к администратору!";
	Иначе
		РезультатЗагрузки = "Загрузка выполнена успешно!";
	КонецЕсли;
	
	Элементы.СинхронизацияДанных.Видимость = Отказ;
	Элементы.Предупреждения.Видимость = НЕ Отказ;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУзелОбмена()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.Ссылка
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК СинхронизацияДанныхЧерезУниверсальныйФормат
	|ГДЕ
	|	СинхронизацияДанныхЧерезУниверсальныйФормат.ЭтотУзел = ЛОЖЬ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		УзелОбмена = Выборка.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти