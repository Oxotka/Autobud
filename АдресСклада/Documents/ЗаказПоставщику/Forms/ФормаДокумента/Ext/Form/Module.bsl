#Область ОбработчикиСобытийФормы

&НаСервере
Процедура АдресСклада_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДополнитьСписокВыбораАдресов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресСклада_ОрганизацияПриИзмененииПосле(Элемент)
	
	АдресСклада_ОрганизацияПриИзмененииПослеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСклада_СкладПриИзмененииПосле(Элемент)
	
	АдресСклада_СкладПриИзмененииПослеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДополнитьСписокВыбораАдресов()

	Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		ЭлементАдреса = Элементы.АдресДоставкиДляПоставщика;
	Иначе
		ЭлементАдреса = Элементы.АдресДоставкиДляПоставщика2;
	КонецЕсли;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Склад);
	
	Адреса = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		МассивОбъектов, 
		Перечисления.ТипыКонтактнойИнформации.Адрес);
	Если Адреса.Количество() > 0 Тогда
		Адрес = Адреса[0];
	Иначе
		Возврат;
	КонецЕсли;
	
	Телефоны = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		МассивОбъектов, 
		Перечисления.ТипыКонтактнойИнформации.Телефон);
		
	Если Телефоны.Количество() > 0 Тогда
		Телефон = Телефоны[0];
	Иначе
		Телефон = Новый Структура("Представление");
	КонецЕсли;
	ЗначениеАдреса = СтрШаблон("%1, %2", Адрес.Представление, Телефон.Представление);
	ПредставлениеАдреса = СтрШаблон("%1: %2, %3", Строка(Адрес.Вид), Адрес.Представление, Телефон.Представление);
	ЭлементАдреса.СписокВыбора.Добавить(ЗначениеАдреса, ПредставлениеАдреса);
	
КонецПроцедуры

&НаСервере
Процедура АдресСклада_ОрганизацияПриИзмененииПослеНаСервере()
	
	ДополнитьСписокВыбораАдресов();
	
КонецПроцедуры

&НаСервере
Процедура АдресСклада_СкладПриИзмененииПослеНаСервере()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		ЭлементДоставки = Элементы.АдресДоставкиДляПоставщика;
	Иначе
		ЭлементДоставки = Элементы.АдресДоставкиДляПоставщика2;
	КонецЕсли;
	
	ПродажиСервер.ЗаполнитьСписокВыбораАдреса(ЭлементДоставки, Объект.Организация);
	ДополнитьСписокВыбораАдресов();
	
	КоличествоЭлементов = ЭлементДоставки.СписокВыбора.Количество();
	Если КоличествоЭлементов <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	Объект.АдресДоставкиДляПоставщика = ЭлементДоставки.СписокВыбора[КоличествоЭлементов-1].Значение;
	
КонецПроцедуры

#КонецОбласти


